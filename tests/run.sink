# (c) Copyright 2016-2018, Sean Connelly (@voidqk), http://sean.cm
# MIT License
# Project Home: https://github.com/voidqk/sink

#
# this script will search for sink executables to test, and run the test suite against them
#

include 'shell'

# TODO: use `path.join` once that works
var path_list = file.script | str.split '/'
list.pop path_list
var tests_dir = path_list | list.join '/'
list.pop path_list
var sink_dir = path_list | list.join '/'
list.push path_list, 'tgt'
var tgt_dir = path_list | list.join '/'
list.push path_list, 'bin.sink'
var bin_sink = path_list | list.join '/'
list.pop path_list
list.push path_list, 'sink'
var sink_posix = path_list | list.join '/'
list.pop path_list
list.push path_list, 'sink.exe'
var sink_win = path_list | list.join '/'
list.pop path_list
list.push path_list, 'driver.js'
var sink_js = path_list | list.join '/'

# search for sink executables to test
var sink_exes = {}
if file.exists sink_posix
	say (str.pad 'Testing POSIX:', 20), sink_posix
	list.push sink_exes, sink_posix
end
if file.exists sink_win
	say (str.pad 'Testing Windows:', 20), sink_win
	list.push sink_exes, sink_win
end
if file.exists sink_js
	say (str.pad 'Testing JavaScript:', 20), sink_js
	list.push sink_exes, sink_js
end
if &sink_exes == 0
	abort 'No sink executables found to test'
end

# get list of tests
var tests = {}
var tests_list = dir.list tests_dir
for var test: tests_list
	if +test[0] && test[1] == '.'
		list.push tests, test
	end
end
for var test: tests_list
	if +test[0] && +test[1] && test[2] == '.'
		list.push tests, test
	end
end

def print_diff answer, actual
	if which 'diff'
		var ansf = tgt_dir ~ '/' ~ 'temp1.txt'
		var actf = tgt_dir ~ '/' ~ 'temp2.txt'
		file.write ansf, answer
		file.write actf, actual
		run 'diff', {'-u', '--label', 'Expected Output', ansf, '--label', 'Actual Output', actf}
	else
		say '>> EXPECTED:'
		say answer
		say '>> ACTUAL:'
		say actual
	end
end

for var test: tests
	var test_dir = tests_dir ~ '/' ~ test                       # TODO: path.join
	var test_num = +test
	var test_name = (test | str.split '.')[1:] | list.join '.'
	var test_script = test_dir ~ '/' ~ test_name ~ '.sink'      # TODO: path.join
	var pass_file = test_dir ~ '/' ~ 'pass.txt'                 # TODO: path.join
	var fail_file = test_dir ~ '/' ~ 'fail.txt'                 # TODO: path.join

	declare test_failed

	var bin_data = nil
	for var sink_exe: sink_exes
		# run the script
		var {status, stdout, stderr} = run sink_exe, {test_script}, nil, nil, 1

		if status == 0
			#
			# script succeeded
			#

			# check stdout
			if !file.exists pass_file
				say stdout
				test_failed 'Expecting script to fail but it passed'
			end
			var answer = file.read pass_file
			if answer != stdout
				print_diff answer, stdout
				test_failed 'Incorrect results'
			end

			# check compiling
			var {cstatus, cstdout, cstderr} = run sink_exe, {'-c', '-d', test_script}, nil, nil, 1
			if cstatus != 0
				say cstderr
				test_failed 'Failed to compile script'
			end
			if bin_data == nil
				bin_data = cstdout
			else
				if bin_data != cstdout
					say 'Compiler 1:', sink_exes[0]
					say 'Compiler 2:', sink_exe
					test_failed 'Mismatched compiler results'
				end
			end

			# check bytecode execution results
			file.write bin_sink, cstdout
			var {rstatus, rstdout, rstderr} = run sink_exe, {bin_sink}, nil, nil, 1
			if rstatus != 0
				say rstderr
				test_failed 'Failed to execute bytecode'
			end
			if answer != rstdout
				print_diff answer, rstdout
				test_failed 'Incorrect bytecode results'
			end
		else
			#
			# script failed
			#

			if file.exists pass_file
				say stderr
				test_failed 'Expecting script to pass but it failed'
			end
		end
	end

	def test_result res
		say (str.pad test_num, -4) ~ '.' ~ (str.pad test_name, 20) ~ res
	end

	def test_failed reason
		test_result 'FAIL'
		abort reason
	end

	# test passed
	test_result 'PASS'
end
